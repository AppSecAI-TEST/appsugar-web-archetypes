buildscript {	
	ext.loadGroovyConfig={ p ->
		if(p == null){
			return new java.util.HashMap()
		}
		def configFile = file("environment.groovy")
		def profileObject = new ConfigSlurper(p).parse(configFile.toURL()).toProperties()
		if(profileObject.isEmpty()){
			throw new InvalidUserDataException("profile not found name is"+p)
		}
		return profileObject
	}
	ext{
		env = loadGroovyConfig("base")
		env.putAll(loadGroovyConfig(System.getProperty("profile")))
		env.putAll(System.properties)
	}
	
	repositories {
		maven{url "http://maven.aliyun.com/nexus/content/groups/public"}
		mavenCentral()
		mavenLocal()
		jcenter()
		maven{url "http://nexus.ggxueche.cn/nexus/content/groups/public/"}
	}
	dependencies {
		classpath ("io.github.divinespear:jpa-schema-gradle-plugin:0.2.3-ext")
		classpath ("com.ferigma:dbunit-gradle-plugin:0.1.0")
		classpath ("${env['test.jdbc.groupId']}:${env['test.jdbc.artifactId']}:${env['test.jdbc.version']}")
		classpath ("org.springframework:springloaded:1.2.6.RELEASE")
	}
}

plugins {
	//gradle release -Prelease.useAutomaticVersion=true -Prelease.releaseVersion=1.0.0-release -Prelease.newVersion=1.0.1-SNAPSHOT
    id "net.researchgate.release" version "2.6.0"
    id "org.hidetake.ssh" version "2.9.0"
    id "org.springframework.boot" version "1.5.4.RELEASE" apply  false
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.9" apply  false
}
apply plugin: "java"

configure(subprojects) { project ->
	apply plugin: "java"
	apply plugin: "org.springframework.boot"
	repositories {
		maven{url "http://maven.aliyun.com/nexus/content/groups/public"}
		mavenCentral()
		mavenLocal()
		maven{url "http://nexus.ggxueche.cn/nexus/content/groups/public/"}
	}
	ext{
		appsugarVersion = "2.4.0-SNAPSHOT"
		appsugarSecurityVersion = "1.0.0.RELEASE"
		junitVersion = "4.12"
		elApiVersion = "3.0.0"
		shiroVersion = "1.3.2"
		ehcacheVersion = "2.10.2"
		jstlVersion = "1.2"
		wro4jVersion = "1.7.9"
		sitemeshVersion = "3.0.1"
		jqueryValidationVersion = "1.15.0"
		jqueryVersion = "2.2.3"
		bootstrapVersion = "3.3.6"
		queryDslVersion = "4.1.4"
		modelMapperVersion = "0.7.8"
	}
	compileJava.options.encoding = env["encoding"]
	compileTestJava.options.encoding = env["encoding"]
	dependencies{
		testCompile ("junit:junit:${junitVersion}")
		testCompile ("javax.el:javax.el-api:${elApiVersion}")
		testCompile ("${env['test.jdbc.groupId']}:${env['test.jdbc.artifactId']}:${env['test.jdbc.version']}")
		testCompile ("org.springframework.boot:spring-boot-starter-test")
		testCompile ("org.appsugar:appsugar-test:${appsugarVersion}")
	}
	processTestResources {
		from(sourceSets.test.resources.srcDirs) {
			include "**/*.properties"
			filter(org.apache.tools.ant.filters.ReplaceTokens,tokens: env)
		}
	}
	test{
		testLogging{
			events "passed", "skipped", "failed", "standardOut", "standardError"
			outputs.upToDateWhen {false}
			showStandardStreams  = true
		}
	}
	bootRepackage.enabled = false
}


project(":appsugar-archetypes-core"){
	apply plugin: "jpa-schema-generate"
	apply plugin: "dbunit"
	apply plugin: "com.ewerk.gradle.plugins.querydsl" 
	
	sourceSets {
	    generated {
	        java {
	            srcDirs = ["src/main/generated"]
	        }
	    }
	}
	
	dependencies{
		compile ("org.appsugar:appsugar-data-jpa:${appsugarVersion}")
		compile ("org.appsugar:appsugar-service:${appsugarVersion}")
		compile ("org.apache.shiro:shiro-spring:${shiroVersion}")
		compile ("org.apache.shiro:shiro-ehcache:${shiroVersion}")
		compile ("org.springframework.boot:spring-boot-starter-data-jpa")
		compile ("org.springframework.boot:spring-boot-starter-validation")
		compile ("org.springframework.boot:spring-boot-starter-aop")
		compile ("com.querydsl:querydsl-jpa:${queryDslVersion}")
	}
	
	generateSchema {
		vendor = "hibernate"
		databaseAction = "drop-and-create"
		scriptAction = "drop-and-create"
		packageToScan = [project.group]
		jdbcDriver = env["test.jdbc.driverClassName"]
		jdbcUrl = env["test.jdbc.url"]
		jdbcUser = env["test.jdbc.username"]
		jdbcPassword = env["test.jdbc.password"]
		properties = ["hibernate.dialect":env["test.hibernate.dialect"]]
	}
		
	dbunit {
		username = env["test.jdbc.username"]
		password = env["test.jdbc.password"]
		url = env["test.jdbc.url"]
		driver = env["test.jdbc.driverClassName"]
		dataTypeFactoryName  = env["test.dbunit.dataTypeFactoryName"]
	}
	
	task populateTestDb(type: com.ferigma.gradle.dbunit.tasks.OperationTask,dependsOn : "generateSchema") {
		sources = [
				new com.ferigma.gradle.dbunit.tasks.source.OperationSource(
						transaction: true, type:env["test.dbunit.operationType"], format: "flat",
						file: "$projectDir"+env["test.dbunit.sampleData"])
		]
	}
	
	test.dependsOn populateTestDb
	querydsl {
	  jpa = true
	  querydslSourcesDir = sourceSets.generated.java.srcDirs.iterator().next()
	}
	compileQuerydsl.options.encoding = env["encoding"]
}



project(":appsugar-archetypes-web"){
	apply plugin: "war"
	
	configurations {
		//spring repackager will pack it in executable jar or war but original not.
    	providedRuntime
	}	
	sourceSets {
	    test {
	        resources {
	        	srcDir  "src/main/webapp"
	            exclude "WEB-INF"
	            exclude "static"
	        }
	    }
	}
	
	dependencies{
		compile project(":appsugar-archetypes-core")
		compile ("org.apache.shiro:shiro-web:${shiroVersion}")
		compile ("org.springframework.boot:spring-boot-starter-web")
		providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
		providedRuntime ("org.apache.tomcat.embed:tomcat-embed-jasper")
		compile ("org.webjars:bootstrap:${bootstrapVersion}")
		compile ("org.webjars:jquery:${jqueryVersion}")
		compile ("org.webjars:jquery-validation:${jqueryValidationVersion}")
		compile ("org.sitemesh:sitemesh:${sitemeshVersion}")
		compile ("ro.isdc.wro4j:wro4j-core:${wro4jVersion}")
		compile ("javax.servlet:jstl:${jstlVersion}")
		compile ("${env['jdbc.groupId']}:${env['jdbc.artifactId']}:${env['jdbc.version']}")
		compile ("org.hibernate:hibernate-ehcache")
		compile ("org.appsugar.security:appsugar-security-form:${appsugarSecurityVersion}")
		compile ("org.appsugar.security:appsugar-security-cas:${appsugarSecurityVersion}")
		compile ("org.appsugar.security:appsugar-security-cas-oauth2:${appsugarSecurityVersion}")
		
	}
	processResources {
		from(sourceSets.main.resources.srcDirs) {
			include '*.properties'
			filter(org.apache.tools.ant.filters.ReplaceTokens,tokens: env)
		}
		doLast{
			ant.delete() {
		        fileset(dir: "${processResources.destinationDir}") {
		            include(name: "**/ApplicationResources*.properties")
		        }
	    	}
		    ant.native2ascii(src: "src/main/resources",
		            dest: "${processResources.destinationDir}",
		            includes: "**/ApplicationResources*.properties",
		            encoding: "UTF-8")
		}
	}
	bootRepackage.enabled = true
	springBoot {
		//execute without java -jar  example  $  ./api.war
    	executable = true
	}
}

project(":appsugar-archetypes-api"){
	apply plugin: "war"
	configurations {
		//spring repackager will pack it in executable jar or war but original not.
    	providedRuntime
	}	
	
	sourceSets {
	    test {
	        resources {
	        	srcDir  "src/main/webapp"
	            exclude "WEB-INF"
	            exclude "static"
	        }
	    }
	}
	
	dependencies{
		compile project(":appsugar-archetypes-core")
		compile ("org.apache.shiro:shiro-web:${shiroVersion}")
		compile ("org.springframework.boot:spring-boot-starter-web")
		providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
		compile ("${env['jdbc.groupId']}:${env['jdbc.artifactId']}:${env['jdbc.version']}")
		compile ("org.hibernate:hibernate-ehcache")
		compile ("org.modelmapper:modelmapper:${modelMapperVersion}")
		
	}
	processResources {
		from(sourceSets.main.resources.srcDirs) {
			include "*.properties"
			filter(org.apache.tools.ant.filters.ReplaceTokens,tokens: env)
		}
		doLast{
			ant.delete() {
		        fileset(dir: "${processResources.destinationDir}") {
		            include(name: "**/ApplicationResources*.properties")
		        }
	    	}
		    ant.native2ascii(src: "src/main/resources",
		            dest: "${processResources.destinationDir}",
		            includes: "**/ApplicationResources*.properties",
		            encoding: "UTF-8")
		}
	}
	test.dependsOn  ":appsugar-archetypes-core:test"
	bootRepackage.enabled = true
	springBoot {
    	executable = true
	}
	ssh.settings {
	  knownHosts = allowAnyHosts
	}
}
